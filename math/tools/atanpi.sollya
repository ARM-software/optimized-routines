// polynomial for approximating atanpif(x)
//
// Copyright (c) 2025, Arm Limited.
// SPDX-License-Identifier: MIT OR Apache-2.0 WITH LLVM-exception

f = atan(x)/pi;
dtype = single;

a = 0x1.0p-126;
b = 1;

deg = 8;

// We look for a polynomial in x^2 such that f(x) ~ x * poly(x^2)
// basically atan(x)/pi ~  c0 * x + c1 * x^3 + c2 * x^5 + ...
backward = proc(poly, d) {
  return d * poly(d * d);
};

// We need to feed remez algorithm with the inverse of the backward transform
// g(y) such that g(x^2) = poly(x^2)
forward = proc(f, d) {
  return f(sqrt(d))/(sqrt(d));
};

// Optimise polynomial iteratively, minimising relative norm (f(x) - poly(x))/f(x)
approx = proc(poly, d) {
  return remez(1 - poly(x) / forward(f, x), deg - d, [a;b], x^d/forward(f, x), 1e-16);
};

p = 0;
poly = 0;
for i from 0 to deg do {
  i;
  p = roundcoefficients(approx(poly,i), [|dtype ...|]);
  poly = poly + x^i*coeff(p,0);
};

display = hexadecimal!;
print("rel error:", dirtyinfnorm(1-backward(poly, x)/f(x), [a;b]));
print("in [", a, b, "]");
for i from 0 to deg do print(coeff(poly, i));
