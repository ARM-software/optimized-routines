// polynomial for approximating single precision e^x.
//
// Copyright (c) 2025, Arm Limited.
// SPDX-License-Identifier: MIT OR Apache-2.0 WITH LLVM-exception

// Parameters
deg = 4; // Neon 5, SVE 2 (fast path) 4 (slow path)
N = 64;  // Neon 1, SVE 64
b = 1/(2*N); // interval
a = -b;
f = exp(x);

// return p that minimizes |f(x) - poly(x) - x^d*p(x)|/|f(x)|
approx = proc(poly, d) {
  return remez(1 - poly(x)/f(x), deg-d, [a;b], x^d/f(x), 1e-10);
};
// return p that minimizes |f(x) - poly(x) - x^d*p(x)|
approx_abs = proc(poly, d) {
  return remez(f(x) - poly(x), deg-d, [a;b], x^d, 1e-10);
};

// first coeff is fixed, iteratively find optimal coeffs
poly = 1+x+(x^2)/2;
for i from 3 to deg do {
  p = roundcoefficients(approx(poly,i), [|SG ...|]);
// p = roundcoefficients(approx_abs(poly,i), [|SG ...|]);
  poly = poly + x^i*coeff(p,0);
};

display = hexadecimal;
print("rel error:", accurateinfnorm(1-poly(x)/f(x), [a;b], 30));
print("abs error:", accurateinfnorm(f(x)-poly(x), [a;b], 30));
print("in [",a,b,"]");
print("coeffs:");
for i from 0 to deg do coeff(poly,i);
